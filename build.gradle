apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'

repositories {
    maven { url 'https://repo.spring.io/libs-release' }
    mavenCentral()
}

group = 'io.spring.gradle'

dependencies {
    compile gradleApi()
    compile localGroovy()
}


dependencies {
    compile 'net.sf.docbook:docbook-xsl:1.78.1:resources@zip'
    compile 'net.sf.docbook:docbook-xml:5.0:xsd-resources@zip'
    compile 'net.sf.docbook:docbook-xsl-saxon:1.0.0'
    compile 'org.apache.xerces:resolver:2.9.1'
    compile 'org.apache.xerces:xercesImpl:2.9.1'
    compile 'saxon:saxon:6.5.3'
    compile 'net.sf.xslthl:xslthl:2.0.1'
    compile 'org.apache.xmlgraphics:fop:2.0@jar'
    compile 'org.apache.xmlgraphics:xmlgraphics-commons:1.3'
    compile 'org.apache.xmlgraphics:batik-bridge:1.7@jar'
    compile 'org.apache.xmlgraphics:batik-util:1.7@jar'
    compile 'org.apache.xmlgraphics:batik-css:1.7@jar'
    compile 'org.apache.xmlgraphics:batik-dom:1.7'
    compile 'org.apache.xmlgraphics:batik-svg-dom:1.7@jar'
    compile 'org.apache.avalon.framework:avalon-framework-api:4.3.1'
}

task copyDocbookResources {
    outputs.dir "${project.buildDir}/docbook"

    doLast() {
        def docbookZip = configurations.compile.files.find { file -> file.name.contains('docbook-xsl-')};

        copy { // copy all images and highlighting files from docbook dist
            from zipTree(docbookZip)
            include 'docbook/images/**'
            include 'docbook/highlighting/**'
            into project.buildDir
        }

        copy { // copy docbook resources from src/main
            from 'src/main/docbook'
            into "${project.buildDir}/docbook"
        }
    }
}


task docbookResourcesZip(type: Zip, dependsOn: copyDocbookResources) {
    from copyDocbookResources.outputs.files
    destinationDir = new File(project.sourceSets.main.output.classesDir, "META-INF")
    archiveName = 'docbook-resources.zip'
}

jar.dependsOn docbookResourcesZip

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}
